%% FILE evaluate.pl.
%% SYSTEM TUC
%% CREATED TA-930528
%% REVISED JB-960706 TA-001017


evaluateq2(nil):-!. 


evaluateq2(R):- 
    value(busflag,true), %% Set by Bustuc Application Program
	 !,
    ieval(R).            %%  interapp.pl 

evaluateq2([R|_]):-  %% NB  Only first of multiple queries. 
    evaluateq(R).    %% TA-000125

%% TUC Query Language Evaluation

evaluateq(do::quit):-!,
    reset,  
    output('Good bye'),
    nl.

evaluateq(do::hello):- !,
    output('Hello'),
    nl. 

evaluateq(do::P):-
    !,
    doall(qev(P)), %% succeed in the end
    nl,nl.


evaluateq(which(X)::P):-
     findq(X,P,Z),   
     !,
     writelist(Z), %% TA-000817
     nl,
     update_focus(Z).


evaluateq(test::P):-

   ( qev(P),
     doubt('Yes','Ja'),!;
     doubt('No','Nei')),
     nl.

evaluateq(explain::P):- 
     output('-----------'),
     evaluateq(test:: P),  %%%%%%%%% not P  ???????????? %% TA-000314
     output('-----------').


evaluateq(howmany(X)::P):-
     !,
     findq(X,P,Z), 
     length(Z,N),
     out(N),nl,
     nl. 

evaluateq(new::_). %% Actually a miss 

evaluateq(nil).  %% In case queryflag =: true, only nil is produced as TQL. %% TA-000202

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


difact(F):-                         %% TA-980301
    value(context_id,UID),
    difact(UID,F).

update_focus([X]):-        
    have(thing,class,X,C),      
    !,                     
    new_focus(X,C).        

update_focus([X]):-        
    X= [U|_],                      % X is a non empty list of
    have(thing,class,U,C),         % C
    !,                             % 
    new_focus(X,set(C)).           % 

update_focus(_).

new_focus(X,M):-      %  Focus is allowed in permament store

    value(context_id,UID),  
    retractall(difact(UID,_ is_the M)),
    retractall(fact0(_ is_the M)),
    (permanence =: 1 -> 
       asserta(fact0(X is_the M));  
       asserta(difact(UID,X is_the M))). %%   % Last Qualified Reference



findq(X,P,Z):- 
    set_of(Y,(qev(P),qdev(X,Y)),Z).


qdev(X,Too_general):-  
    var(X),
    !,
    (language(english) -> 
        Too_general='Too general';
        Too_general='For generelt').

/*  replace a Skolem constant with a class %% TA-0010117
qdev(X,Y):- 
    unskolemize(X,Y),
    !. 

*/

qdev(X,X).

unskolemize(sk(J),AMan):- 
    !,
    fact(sk(J) isa Man), 
    klassing(Man,AMan).       %% turbo.pl


disqev(P) :-  
    qev(nil,P).     %% only allowed to refer to discourse elements

qev(P):-qev(0,P). 

qev(N,(X,Y)):-!,
    qev(N,X),
    qev(N,Y) .


qev(N,not X) :- !,
    \+ qev(N,X).

qev(_,true) :- !.

qev(N,set_of(X,Y,Z)):-!, 
    set_of(X,qev(N,Y),Z).

qev(N,aggregate(MAX,X,Y,P,ML)):-!, 
    set_of(Y-X,qev(N,P),Z1),        %% X:Y ==> Y-X  (keysort notation)
    keysort(Z1,Z2),               %% even if not necessary
    aggregate(MAX,Z2,ML).         %% utility.pl

qev(_,quant(CR/M,X)::P):- !,  
     findq(X,P,Z),  
     length(Z,M1),
     testquant(CR,M1,M).


qev(N,be/AB/CD/_) :- 
    nonvar(AB),
    AB=(A,B),
    !,
    CD=(C,D),
    qev(N,be/A/C/_),
    qev(N,be/B/D/_). 


qev(N,P):-
    factrule(N,P).

%%%%%%%%%%%%%%%%%%%%%%%%%%% 

factrule(N,X):- 
    ground(X),
    !,
    gfact(N,X).

factrule(N,X):- 
    vfact(N,X). % hfact ==> vfact


gfact(N,X) :- vfact(N,X),!. % ONCE !


vfact(nil,X isa Y):- !,     % for dialog query
    dialog_referent(X,C),   % X must be a dialog referent
    subclass(C,Y).          %   and an instance

vfact(nil,X):- !,difact(X). %% TA-980301


vfact(N,X):-  
   rule(N,X).

vfact(_,X isa Y):- 
     !, 
     instant(X,Y). 

vfact(_,Z):-  \+ ( Z = _ isa _), %% TA-971109    
   fact(Z).

dialog_referent(X,C) :-    %% TA-970529
    fakt(X is_the C);      %  dynamic or static 
    difact(X isa C).       %% TA-980301


rule(N,Y):- 
   leveltest(N,N1),
   ( X => Y), 
   qev(N1,X).

%% Hierarchy of facts 
 
fact(X):-               %  Predefined interpretations 
    value(deflag,true), % AD HOC FLAG  %% DEF IS  FOR COMMANDS
    nonvar(X),                         %% John shows a car \= commans
    X=Y/_,
    nonvar(Y),
 
    def X .   

fact(X):- fact0(X).     %  Semi permanent

fact(X):- difact(X).     %  Dynamic Discourse facts %% TA-980301

fact(X isa Y):- !,      %  Default type definitions
     X isa Y .   


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

included(X,Z):-
    fact(nrel/in/_/_/X/Y), 
    included(Y,Z).
included(X,X). % Outside in



instant(X,T):-     
     var(X),  %% TA-980212
     testclass(T),

     !.  %% X= ' not listable '. 

instant(X,C):-  
    fact(X isa C).

instant(X,Z) :- % bottom up
    (Y ako Z), %% TA-970725
    instant(X,Y).
    

valof(X,Y):-
    number(X),
    !,
    X=Y.


leveltest(M,N):-
    value(maxdepth,K), %% maxlevel(K),
    M < K,
    N is M + 1.

fakt(X):-
         difact(X); %% dynamic TA-980301
         fact0(X). %% static

%%%%%%%%%%%%%%%  THE END %%%%%%%%%%%%%%%



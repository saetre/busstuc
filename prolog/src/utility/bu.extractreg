%% FILE extractreg.pl
%% SYSTEM BussTUC
%% CREATED TA-981221
%% REVISED TA-071127


/********

 Procedure extractreg is a part of busstuc, and is 
 run in main directory  ( tele/buster/ )

 REGTOP data has to be  put in the  directory

     tele/buster/regtop/

% busestuc.sav  
?-extractreg.
?-halt.



 FILES  =  R0021     (typically)
 domain =  tt        (normally)
 module =  tt06may17 (example)

 All files are put in  tele/buster/database/<module>/

%% NB !!

--  At present, the extension of the files are supposed to be in 

    lower case (dko,hpl,tix,tda) --
    upper case (DKO,HPL,TIX,TDA) --

*/


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


:-use_module(library(system)). 


:-dynamic  filename/1, 
           domainname/1, 
           modulename/1, 
           extensiontype/1, %% TA-070614
           dkodate/1, 
           dkoday/1, 
           val/1,
           daycode/2,  %% TA-051122
           composite_stat0/3,
           ex_regbus/1,       %% TA-060529
           ex_passes3/5,      %% local copies
           ex_departureday/4,
           ex_route/3,
           ntourstops/2,
           ex_cutloopextension/5,
           ex_cutloop_extend7/7.  %%

cleanslate :-

         retractall(filename(_)), 
         retractall(domainname(_)), 
         retractall(modulename(_)), 
         retractall(extensiontype(_)), %% TA-070614
         retractall(dkodate(_)), 
         retractall(dkoday(_)), 
         retractall(val(_)),
         retractall(daycode(_,_)), 
         retractall(composite_stat0(_,_,_)),
         retractall(ex_regbus(_)),   
         retractall(ex_passes3(_,_,_,_,_)),   
         retractall(ex_departureday(_,_,_,_)),
         retractall(ex_route(_,_,_)),
         retractall(ntourstops(_,_)),
         retractall(ex_cutloopextension(_,_,_,_,_)),
         retractall(ex_cutloop_extend7(_,_,_,_,_,_,_)).





%% run:- extractreg. // %% TA-060430 

extractreg:-

    write('  File names:  '),    read_name(R00XY),  

    write('  Extensiontype: (dko | DKO)  '), read_name(XXX),

    write('  Domain: '),    read_name(Domain), 

    write('  Module: '),    read_name(Module),

    extractreg(R00XY,XXX,Domain,Module).




extractreg(R00XY,XXX,Domain,Module):-

    cleanslate, 

    preparesource(R00XY,XXX),
    preparedomain(Domain),
    preparedestination(Module),
    retractall(dkodate(_)),
    retractall(dkoday(_)),
    retractall(daycode(_,_)), %% TA-051122

    extractreg1.

/*
preparesource(R0099,___):- var(R0099),!, 
     write('Use quotes around filename'),nl,abort.
*/

preparesource(R0099,XXX):- 
    nonvar(R0099),
    retractall(filename(_)),
    append_atomlist(['regtop/',R0099],RT0099),
    assert(filename(RT0099)),
    retractall(extensiontype(_)),
    assert(extensiontype(XXX)).
  
preparedomain(Domain):-
     var(Domain),!, write('Variable Domain name'),nl,abort.

preparedomain(Domain):-
    nonvar(Domain),
    retractall(domainname(_)),
    assert(domainname(Domain)).
 
  
preparedestination(Module):-
     var(Module),!, write('Variable Module name'),nl,abort.

preparedestination(Module):-
    nonvar(Module),
    retractall(modulename(_)),
    assert(modulename(Module)),
    makedir(Module).

makedir(Module):-   %% run in  (buster ==>  buster/database/Module/
    nonvar(Module),
    append_atomlist(['mkdir database/',Module],MK), %% 
    shell(MK), % gets an error message if exists
    !.

makedir(Module):- 
    write(Module),
    write(' already exists, OK'),nl.



%% NEW:  Daycodes are processed automatically

% Converts Route Files in REGTOP format to
% Bus Tables in BUSTUC Prolog format


%% NB  Includes daycodes for easter, which is AD HOC


%% Program written in Sicstus Prolog


/*
 

Compile and run in a directory containing the files

'R0021.HPL',
'R0021.TDA' 
'R0021.TIX'
'R0021.DKO'


The Progam creates the files in the same directory.

reghpl.pl  % Station number and names
regpas.pl  % Buss passes
regdep.pl  % Bus tour departures
regbus.pl  % Bus file
regcomp.pl % Standard compositional names


All the reg*.pl files are eventually compiled, and constitues the busbase.

The rest is history.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


?-compile(extractreg).


?-  extract(dko).
?-  extract(hpl). %%  Indexed on Pred in template below
?-  extract(tix).
?-  extract(tda).


Temporary files created are

  hpl.pl 
  tix.pl 
  tda.pl

2................................................


Make no  changes, because BussTUC is dependent on the names given.



3..................................................

Compile these files  (may take a  minute) 


4.................................................

You may wish to store the result  temporarily (to save time)

 ?-save_program(tijuana)

4B..........

If Sicstus has been exited in the meantime

% tijuana.sav


5.................................................


?-crepasdep. %% 1 min


Files created are

  regdko.pl
  reghpl.pl
  regpas.pl
  regdep.pl
  regbus.pl
  regcomp.pl


6..................................

?-halt.

*/




%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%



team_name(tt).
team_name(team).


extractreg1 :-

    nl,write('Wait a minute'),nl,nl,

%%  Indexed on Pred in template below

    extract(dko),  output('dko extracted'),
    extract(hpl),  output('hpl extracted'),
    extract(tix),  output('tix extracted'),
    extract(tda),  output('tda extracted'),


    consult('regtop/dko'),     %     1 sec
    consult('regtop/hpl'),     %     3 sec     
    consult('regtop/tix'),     %    72 sec                
    consult('regtop/tda'),     %   349 sec

    crepasdep,

    nl,write('Finish'),nl,nl,

    write('    halt'),nl.

    



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% template(filenameout,predname,[list of byte sizes]).



template('regtop/dko.pl',dko,[+3,+1,+4, %% +392]). % Except 1. head line
     7,7,7,7,  %% Determined by the first 4 weeks
     364]).    %% 392-4*7

template('regtop/hpl.pl',hpl,[-4,+8,30,-5,-7,-35]). %%  8 sifret hplnr.

template('regtop/tix.pl',tix,[-4, +4, +4,-3, +4, -24,+4,+3,+7,-2]).

/*
00210254000100200110230000000000     2541290725040001025800 
----++++++++---++++------------------------++++++++++++++--
-4  +4  +4  -3 +4  -24                     +4  +3 +7     -2]

departureday(bus_254_1,10258,725,d1111100).
*/

%                                  16830303002400200510210000000000     3032001935065000104600
%                                      Lin Rout   DayC     ...
template('regtop/tda.pl',tda,[+8,+3,+3,+6]). %%    8 sifret hplnr.



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% template2('R0099.DKO','regtop/dko.pl',dko,[...]).

template2(File1,File2,Pred,List):-
      filename(R0099),
      extensiontype(XXX),
      !,
      biglet(XXX,Pred,PRED),
      append_atomlist([R0099,'.',PRED],File1),
      template(File2, Pred,List).


biglet(dko,dko,'dko').  %% AD HOC %% TA-070613
biglet(dko,hpl,'hpl').
biglet(dko,tix,'tix').
biglet(dko,tda,'tda').



biglet('DKO',dko,'DKO').
biglet('DKO',hpl,'HPL').
biglet('DKO',tix,'TIX').
biglet('DKO',tda,'TDA').


biglet(xxx,dko,'dko').  %% AD HOC %% TA-070613
biglet(xxx,hpl,'hpl').
biglet(xxx,tix,'tix').
biglet(xxx,tda,'tda').



biglet('XXX',dko,'DKO').
biglet('XXX',hpl,'HPL').
biglet('XXX',tix,'TIX').
biglet('XXX',tda,'TDA').


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%



extract(Pred):- 

    resetcount,

    template2(File1,File2,Pred,Templ), %% use given name

    see(File1),

    dummyhead(Pred),

    tell(File2),

    repeat,

        exinitread(U),

        exprocess(Pred,U,Templ), %% TA-060501
 
        U == end_of_file,       
    !,
    told,
    seen.


dummyhead(dko):-  %% Read Date
    !,
    exinitread(Date),  
    handledkodate(Date).

dummyhead(_).


handledkodate(DkoDate):-  
   convertdko(DkoDate,Date),
   assert(dkodate(Date)),

   weekday(Date,Day),  
   assert(dkoday(Day)).

convertdko([D1,D2,D3,D4,D5,D6|_],date(YYYY,MM,DD)):-
    YYYY is 2000 + (D1-48)*10 + D2-48,
    MM is (D3 - 48)*10 + D4-48,
    DD is (D5 - 48)*10 + D6-48.
 


exprocess(_,end_of_file,_):-!.
exprocess(Pred,U,Templ) :-
    corvett(Templ,U,Ulist),
    count(N1),
    Predx =.. [Pred,N1|Ulist],
    convertpred(Pred,Predx,Predy),  % Pred Specific conversion
    writefact(Predy).


corvett([N|NS],US,[V|VS]):- %% Add Info
    N1 is N,
    N > 0,
    firstlist(N1,US,NU,Rest),
    !,
    name(V,NU),
    corvett(NS,Rest,VS).


corvett([N|NS],US,VS):-     %% Skip Info
    N1 is -N,
    N1 > 0,
    firstlist(N1,US,_,Rest),
    !,
    corvett(NS,Rest,VS).


corvett([],_,[]).

%% Convertpred   Pred specific conversion

convertpred(hpl,hpl(_SeqNo,Stnr,Text),hpl(Stnr,Standard,XStandard,Official)) :-
   !,
   makestaname(Text,Standard),
   makexstaname(Standard,XStandard),   
   makeofficial(Text,Official).


convertpred(dko,X,X):-!.


convertpred(_,X,X). %% Default


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

makeofficial(Text,Offname):- %% Default Official station name.
                             %% Redefined by specname if necessary

    name(Text,List),
    reverse(List,Tsil),
    remblanks(Tsil,Bing),
    reverse(Bing,Bong),

    cleaniso(Bong,_Bongo),

    skipblanks(_Bongo,Bongo), %% TA-060807

    name(Offname,Bongo).

remblanks([32|A],B):-!,
    remblanks(A,B).
remblanks(A,A).


cleaniso([],[]).
cleaniso([A|B],[A1|B1]):-
    iso0(A,A1),
    cleaniso(B,B1).

iso0(X,Y):-iso(X,Y),!.
iso0(X,X).


makestaname(Text,Stan):-
    name(Text,ASCII),
    purgate0(ASCII,BSCII), %% TA-060807
    name(Stan,BSCII).



makexstaname(Stan,XStan):-
    domainname(TT),
  
(team_name(TT)-> Prefix=''; 
    append_atomlist([TT,'_'],Prefix)),

    append_atomlist([Prefix,Stan],XStan).


purgate0(Z,Rest):- %% initial blanks are ignored, other blanks assembled to '_'
    skipblanks(Z,Z1),
    purgate(Z1,Rest).

purgate([A|Z],Rest ):-
    skipch(A),             %% appostrophes etc.
    !,
    purgate(Z,Rest).

purgate([Blank|Z],Rest ):-
    exblank(Blank),   %% TA-060501
    !,
    skipblanks(Z,Z1),
    purgate1(Z1,Rest).

purgate([BIG|Z],[Small|Rest]):-
    BIG >= 65,BIG =< 90,
    !,
    Small is BIG+32,
    purgate(Z,Rest).

purgate([A|Z],[B|Rest]):-
    special(A,B),
    !,
    purgate(Z,Rest).     


purgate([],[]).    


purgate1([],[]):-!.
purgate1(Z1,[95|Rest]):-
    purgate(Z1,Rest).


exblank(32). % bl

exblank(44). % ,  
exblank(45). % -
exblank(46). % .
exblank(47). % /  
exblank(95). % _




skipch(40). % (
skipch(41). % )

skipch(39). % '
skipch(96). % `

%% Obviously wrong coded characters appear

% ø and Ø seems to  be replaced by o and O,
% Big Æ does not occur

% Lower case character conversion

special(198,230).    %%  Æ-> æ
special(216,248).    %%  Ø-> ø    
special(197,229).    %%  Å-> å

special(145,230).       %% \221  -> æ
special(146,230).       %% \222  -> Æ -> æ

special(155,248).       %% \233 -> ø
special(157,248).       %% \235 -> ø

special(134,229).       %% \206 -> å
special(143,229).       %% \217 -> Å (197)  -> å

special(X,X).  %% Default



iso(145,230).           %% \221 -> æ
iso(146,198).           %% \222  ->    Æ -> æ

iso(155,248).           %% \233 -> ø
iso(157,216).           %% \235 -> Ø

iso(134,229).           %% \206 -> å
iso(143,197).           %% \217 -> Å (197)  -> å



skipblanks([B|X],Y):-
    blank(B),
    !,
    skipblanks(X,Y).

skipblanks(X,X).



/*****

% create passdep1 from   tix.pl and tda.pl

%  compile tix.pl and tda.pl first

tix(N,Line,Route,Daycode,Deptime,NoPosts,Indextda)
tix(1,2,1,3,620,14,24).


tda(N,Statnr,Arrtime,Deptime,Distance).
tda(1,3770,999,0,0).


********/



crepasdep:-
    credko,    output('dko finished'),
    crehpl,    output('hpl finished'),
    crecomp,   output('comp finished'),
    crepas,    output('pas finished'),
    credep,    output('dep finished'),
    crebus,    output('bus finished'),

    crecut1,   output('cut finished').


crecut1 :-
   tellmodule('regcut.pl'),
   output('%%% regcut.pl %%%'),

   ex_create_cutloop_extend1,

   ex_create_ribs1,
   ex_create_qz1,

   told.

%% Delta is displacement in seq.no,  W is added waiting time

ex_create_cutloop_extend1:-

    for(ex_cut_rib(Station,_Rid1,Trace1,_Rid2,Trace2,Delta,W),
(

   ( (ex_cutloopextension(Station,Trace1,Trace2,Delta,W1),W1 > W) -> %% keep earliest
      (retract(ex_cutloopextension(Station,Trace1,Trace2,Delta,W1)),
        assert(ex_cutloopextension(Station,Trace1,Trace2,Delta,W)))
     ;

      \+ ex_cutloopextension(Station,Trace1,Trace2,Delta,_) -> 
         assert(ex_cutloopextension(Station,Trace1,Trace2,Delta,W))
     ;
         true)
)),

                                               %% should be used only if otherwise empty
   writepred(cutloop_extend(nowhere,0,0,0,0)), %% avoid empty cutloop_extend %% TA-070613 
   dumppredas(ex_cutloopextension( Station,Trace1,Trace2,Delta,W),
                   cutloop_extend( Station,Trace1,Trace2,Delta,W)). 


%% NB W is difference between arrival time for Trace1 and departure time for Trace2

ex_cut_rib(Station,Rid1,Trace1,Rid2,Trace2,Delta,W):-
    cutloop_station(Bus,Station),  
        ex_passes3(Trace1,Station,Seq,Arr1,_Dep1),
    Seq > 1, % at least not the first
       ex_departureday(Rid1,Trace1,Time1,Day),
       ex_route(Rid1,Bus,_),
    addtotime(Time1,Arr1,TimeX), %% arr time 
       ex_passes3(Trace2,Station,1,_Arr2,0), %  _Dep2=0
       ex_departureday(Rid2,Trace2,Time2,Day), %% dep time 
       ex_route(Rid2,Bus,_), %% same bus
    TimeX =< Time2,  %% maybe the same 
    addtotime(TimeX,9,TimeY), % =< 9  minutes waiting time // bus 36 1455
    Time2 =< TimeY,
       ntourstops(Trace1,Delta),
    difftime(Time2,Time1, W).     %% add 



ex_create_ribs1 :-
    set_of(ex_cutloop_extend7(Station,Rid1,Trace1,Rid2,Trace2,Delta,W), 
                   ex_cut_rib(Station,Rid1,Trace1,Rid2,Trace2,Delta,W),
          Z),
    assertpredlist(Z).



ex_create_qz1:-
    ex_settkand(Z),      % ===> ex_q
    ex_settcuts(Z,Z2),   % ex_q ===> ex_qz
    assertpredlist(Z2),  % ===> ex_qz
    dumppredas(ex_qz(B,C,D,E),
           cutset(   B,C,D,E)). 




ex_settkand(Z):-  % ===> ex_q
    set_of(ex_q(Asbjørnsensgt,Trace1,Trace2),     
       ex_cutloop_extend7(Asbjørnsensgt,_Rid1,Trace1,_Rid2,Trace2,_Delta,_W),Z).



ex_settcuts(Z,DummyZ2):- % ex_q ===> ex_qz
  set_of(ex_qz(Asbjørnsensgt,Trace1,Trace2,QQQ),
       ( member(ex_q(Asbjørnsensgt,Trace1,Trace2),Z),
            set_of(toupes(Rid1,Rid2,Delta,W),
                  ex_cutloop_extend7(Asbjørnsensgt,Rid1,Trace1,Rid2,Trace2,Delta,W), 
                  QQQ)),
        Z2),
        ensure_nonempty_cutset(Z2,DummyZ2).%% avoid empty if no cutloop %% TA-070614

ensure_nonempty_cutset([],[ex_qz(nil,0,0,[])]):-!.
ensure_nonempty_cutset(Z,Z).





   
% Compute Daycodes
% Normal daycodes come in sequences of 7 (bits), and start on the date given in dko.
% Normally, the first 4 weeks determine the kind of code, either special day or 
%  repeated  (every)  saturday etc.


%%%%  IF codes start om monday, these are normal


% every daycode is marked   dxxxxxxx


/*

1111111  -> everyday     d1111111
1111100  -> workday      d1111100 
0000001  -> sunday       d0000001
0000010  -> saturday     d0000010

For 1 day holiday routes starting on that day

1000000  -> holiday // kept unchanged 


For special easter rules, starting on skjærtorsdag

%% COUNTING from first day.
%% in EASTER, this is skjærtorsdag

0010000  -> eastereve  %%  Påskeaften  // kept unchanged 
1101000  -> easterhol  %%  Easter holidays // To,Fr,Sø  // kept unchanged 


Otherwise (single)      
         -> holiday    %% Default

*/



%% These daycodes are normal, starting on monday

daymap(monday,[X, X, X, X], X).     %% 4 weeks

daymap(monday,[X, X, X, 0], X).     %% 3 weeks

daymap(monday,[X, X, 0, 0], X).     %% 2 weeks

%% daymap(monday,[X, 0, 0, 0], X).     %% 1 week // NO, if holidya is on monday
                                       %% TA-071227


%% Normal routes starting on day \= monday, e.g. 1.april 2004 

daymap(thursday,[_       ,_       ,_      ,0001000], 0000001). % sunday  
daymap(thursday,[_       ,_       ,_      ,0010000], 0000010). % saturday 
daymap(thursday,[_       ,_       ,_      ,1100111], 1111100). % workday
daymap(thursday,[_       ,_       ,_      ,1111111], 1111111). % everyday


%% Abnormal routes, starting om thursday (Skjærtorsdag) %% Hazard

% For holidays, the route module is found by route planner.
% Then all "days" in route module period is allowed 

daymap(thursday,[0010000,      0,     0,       0], 1111111). %% eastereve).  %% TA-071127
daymap(thursday,[1101000,      0,     0,       0], 1111111). %% easterhol).  


daymap(_,[_,      _,     _,       _],1111111). %% holiday).  %% TA-071127 %% Single Holiday

%% all days in period which are limited by route date limits!!!
                                                                       

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


tellmodule2(TTP,F):- %% TA-080201
    append_atomlist(['database/', TTP,'/',F],File),
    tell(File).


tellmodule(F):-
    modulename(TTP),  
    append_atomlist(['database/', TTP,'/',F],File),
    tell(File).


credko :-


   for(dko(_,_,_,No,A,B,C,D,_),       %% NB not equal sequence number
       assertdaycode(No, [A,B,C,D])), %%

   tellmodule('regdko.pl'),
  
   dumppredas(dko(A,B,C,D,E,F,G,H,I),dko(A,B,C,D,E,F,G,H,I)), 

%%    listing(dko),  %% TA-060518
   told,

   nl,    %% TA-060726
   listing(daycode). %% Just for check


assertdaycode(N,List):-  
    dkoday(DkoDay),

    daymapcode(DkoDay,List,DayCode), %% TA-071127  
    !,
    assert(daycode(N,DayCode)).


daymapcode(DkoDay,List,DayCode) :-  %% TA-071127
    daymap(DkoDay,List,DayList),    %% 100000 -> d100000
    number(DayList),
    !,
    assemblebits(DayList,DayCode).

daymapcode(DkoDay,List,DayCode) :- %% TA-071127
     daymap(DkoDay,List,DayCode).  %% holiday -> holiday


% create station catalogue 

crehpl:-
   tellmodule('reghpl.pl'),

   dumppredas(hpl(X,Y,U,V),hpl(X,Y,U,V)), %% makeauxtables.pl %% TA-060518
   
%%    listing(hpl), 

   told.


% create a list of composite names
  

crecomp :-
    for( (hpl(_,K,_,_),hushname(K,K1,L)), 
              remember(composite_stat0(K1,L,K))),  %% TA-061022

    streethash,                                    %% local

    tellmodule('regcomp.pl'), 
    dumppredas(composite_stat0(K1,L,K),composite_stat(K1,L,K)),
    told.




streethash :-
    for(  (composite_stat0(A,List,Name), hashstreetlist(A,List,A1,List1)),
             remember(composite_stat0(A1,List1,Name))). %% TA-061022


hashstreetlist(A,List,A1,List1):-
    substreetlist([A|List],[A1|List1]),
    ([A|List] \== [A1|List1]).
    

% ambles veg --> ambles_street


substreetlist([A,V|K],[AV|K1]):-
    exstreet1(V),
    mixnames(A,V,AV),
    substreetlist(K,K1).

substreetlist([V|K],[street|K1]):- % (ambles) veg 3 -> (ambles) street 3
    exstreet1(V),
    !,
    substreetlist(K,K1).


substreetlist([V|K],[S|K1]):- % ambleveg 3 ->  amblestreet 3
    exsubstreet0(V,S),
    substreetlist(K,K1).


substreetlist([V|K],[Amble,street|K1]):- % ambleveg 3 -> amble street 3
    exsubstreet1(V,S),
    ends_with(S,Amble,'_street'),  %% utility.pl
    substreetlist(K,K1).

substreetlist([],[]).


mixnames(Ambles,Veg,AmblesVeg) :- 
    append_atoms(Ambles,Veg,AmblesVeg).

mixnames(Amble,_,AmbleStreet)  :- 
    append_atoms(Amble,'_street',AmbleStreet).

mixnames(Ambles,_,AmblesStreet):-
    ends_with(Ambles,_Amble,s),
    append_atoms(Ambles,'_street',AmblesStreet).


% % % % % % % % % % % % % % % % % % % % % % % % % % % % 


crepas :-

% create a list of different tours

   set_of(lx(X,L),tixrelevant(_,_,_,_,_,L,X),List),

   cont1(List).



cont1(List):-

for( 
    member(lx(X,L),List),

     ( assert(ntourstops(X,L)),   %% TA-060526 % La regpas inneholde ntourstops(1,3) 

      index_block(X,L,IndexList),

      set_of( tda(N,Statnr,Arr,Dep,Distance),(member(N,IndexList), tda(N,Statnr,Arr,Dep,Distance)),
                  TDAList),

     for( member(tda(N,Statnr,Arr,Dep,Distance),TDAList),
          (
           mini(Arr,Dep,Arr1,Dep1), 
           hpl(Statnr,_Statname,XStatname,_), 
           SeqNo is (N-X+1),  
           assert(ex_passes3(X,XStatname,SeqNo,Arr1,Dep1)) %% TA-060526
          )   
        )
    )
  ),

  tellmodule('regpas.pl'), 

  dumppredas(ntourstops(X,L),ntourstops(X,L)),

  dumppredas(ex_passes3(X,XStatname,SeqNo,Arr1,Dep1),
             passes3(X,XStatname,SeqNo,Arr1,Dep1)),

  told.




%%%

% create a file of route  departures

credep :-
 

    for( (tixrelevant(_,R,L,D,Time,_,Route), 
          daycode(D,Day),
          makerid(R,L,RL)),
          remember(ex_departureday(RL,Route,Time,Day)) %% TA-061022
         ),

    tellmodule('regdep.pl'),

    dumppredas(ex_departureday(RL,Route,Time,Day),
               departureday(RL,Route,Time,Day)),

    told.



% create a list of buses and RID to buses

crebus:-
     
    set_of(X,bis(X),Z),
    for(member(U,Z),remember(ex_regbus(U))), %% TA-061022
     
    for( (tixrelevant(_,R,L,_,_Time,_,_Route), 
          makerid(R,L,RL),
          creaname(R,L,Name)),
          remember(ex_route(RL,R,Name))), %% TA-061022

     tellmodule('regbus.pl'), 

     dumppredas(ex_regbus(U),regbus(U)),

     dumppredas(ex_route(RL,R,Name),route(RL,R,Name)),

     told.


crecut:-  %% TA-0601518

     modulename(TTP), 
   
     tellmodule('regcut.pl'), 

     createcutribs1(TTP),  %% makeaustables.pl

     told.

  

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%



hushname(C,K1,L):-
   name(C,Alist),
   scanscore(Alist,Clist),
   distrikt(Clist,K1,L).

distrikt([C|D],C,D). %% :- (D \== []). % remove reduns afterwards


scanscore([],[]):-!.


scanscore(L,[FC|Rest]):-
    !,
    split_(L,H,L2),
    scanscore(L2,Rest),
    name(FC,H).



split_([A|B],[A|C],R):-
    \+ exunderscore(A),    %% TA-060501
    split_(B,C,R).

split_([A|B],[], B):-
    exunderscore(A).       %% TA-060619

split_([],[],[]).

exunderscore(95).





%-------------------------------------------------

tixrelevant(A,X,C,D,E,F,G):-  
    tix(A,X,C,D,E,F,G).
%%%%%%    X < 100.        %% relevant bus numbers



bis(X):- tixrelevant(_,X,_,_,_,_,_). 

creaname(R,_,R).

   
makerid(A,B,R_A_B):-
    append_atoms('bus_',A,RA),
    append_atoms(RA,'_',RA_),
    append_atoms(RA_,B,R_A_B).
 

% Ad Hoc  999 means dummy first arrival / last departure
% Normalised to same time


mini(999,Dep,Dep,Dep):-!.    %% First departure, keep Arrtime= Deptime

%% mini(Arr,999,Arr,Arr):-!. %% Last arrival,  Deptime=999 %% TA-061115

mini(Arr,Dep,Arr,Dep).


% Some utilities


read_name(H):-  %% NO . necessary, only for old time prologers
    read_line(J),
    stripoff_lastdot(J,J1),
    atom_codes(H,J1).

stripoff_lastdot(J,K1):-
   append(K1,[46],J),
   !.
stripoff_lastdot(J,J).




exinitread(U):-
     get0(K1), 
	  (K1 == -1 -> % 
      U= end_of_file;
      exreadrestline(K1,U)).

exreadrestline(T,[]):- 
    T=10,  %% CR
    !.

exreadrestline(K,[K|U]):-
    get0(K1), % blanks, comments (evt returns CR)
    exreadrestline(K1,U).




firstlist(N,[K|Y],[K|Z],Rest):-
    N > 0,
    !,
    M is N-1,
    firstlist(M,Y,Z,Rest).

firstlist(_,L,[],L).


resetcount :- retractall(val(_)),
              assert(val(0)).


count(N1):- retract(val(N)),
           N1 is N+1,
           assert(val(N1)).


writefact(P):- 
    writeq(P),write('.'),nl.




%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

exsubstreet0(X,Y):- %% BOTH ALTERNATIVES
    exsubstreet1(X,Y).
exsubstreet0(X,X).


exsubstreet(X,Y):-  %% ONLY 1 IF SUBSTITUTION, OTHERWISE SAME
    exsubstreet1(X,Y),
    !.                    %% <--- !
exsubstreet(X,X).

exsubstreet1(Oslov,Oslo_street):- %% ONLY IF SUBSTITUTION
    exstreetsyn(V),
    ends_with(Oslov,Oslo,V),
    ends_with(Oslo_street,Oslo,'_street'). 


% % % % %

exstreetsyn(X):-
    exstreet1(X)
    ;
    exsstreet1(X).

exstreet1(gata).
exstreet1(gate).
exstreet1(gaten).
exstreet1(gt).
exstreet1(v).
exstreet1(veg).
exstreet1(vegen).
exstreet1(vei).
exstreet1(veien).
exstreet1(vg).
exstreet1(vn).


exsstreet1(sgata).
exsstreet1(sgate).
exsstreet1(sgaten).
exsstreet1(sgt).
exsstreet1(sv).
exsstreet1(sveg).
exsstreet1(svegen).
exsstreet1(svei).
exsstreet1(sveien).
exsstreet1(svg).
exsstreet1(svn).




%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% I am not proud of this (TA)

bitnod(X,N,A):-
   bitno(X,N,B),
   digat(B,A).
    
digat(0,'0').
digat(1,'1').


bitno(X,N,B):-
     pow10(N,TeN),
     N1 is N-1,
     pow10(N1,TeN1),

     Y is X//TeN,
     Z is X - TeN*Y,

     B is Z//TeN1.


assemblebits(X,DA):-
     bitnod(X,7,A1),
     bitnod(X,6,A2),   
     bitnod(X,5,A3),   
     bitnod(X,4,A4),   
     bitnod(X,3,A5),
     bitnod(X,2,A6),
     bitnod(X,1,A7),

    concat_atomlist([d,A1,A2,A3,A4,A5,A6,A7],DA). %% New/ utility 



pow10(0,1).
pow10(1,10).
pow10(2,100).
pow10(3,1000).
pow10(4,10000).
pow10(5,100000).
pow10(6,1000000).
pow10(7,10000000).


%%%%%%%



/*
tda(N,Statnr,Arrtime,Deptime,Distance).
tda(1,3770,999,0,0).
*/



index_block(X,N,XNL):-
    U is X+N,
    make_block_index_list(X,U,XNL).

make_block_index_list(X,U,XNL):-
    set_of(Y,insegment(X,U,Y),XNL).



insegment(M,U,M):- M < U.
insegment(M,U,M2):-
     M1 is M+1,
     M1 <U,
     insegment(M1,U,M2).

     
